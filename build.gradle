buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven {
            name = 'sponge'
            url = 'https://repo.spongepowered.org/maven'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.wynprice.cursemaven:CurseMaven:2.1.1'
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.wynprice.cursemaven'
apply plugin: 'org.spongepowered.mixin'

version = "1.12.2-0.0.21.8"
group = "com.bewitchment"
archivesBaseName = "bewitchment"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_39"
    clientRunArgs += "--username=Bewitchment"
    clientJvmArgs += "-Dfml.coreMods.load=com.bewitchment.core.BewitchmentFMLLoadingPlugin"
    serverJvmArgs += "-Dfml.coreMods.load=com.bewitchment.core.BewitchmentFMLLoadingPlugin"
}

repositories {
    flatDir {
        dirs 'libs'
    }
    maven { url = "http://dvs1.progwml6.com/files/maven" }
    maven { url = "https://minecraft.curseforge.com/api/maven" }
    maven { url "http://maven.tterrag.com" }
    maven { url "https://maven.blamejared.com" }
    maven { url "https://maven.mcmoddev.com/" }
    maven { url "http://repo.spongepowered.org/maven" }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    deobfProvided "mezz.jei:jei_1.12.2:4.15.0.293"
    deobfProvided "thaumcraft:Thaumcraft:1.12.2:6.1.BETA26"
    deobfProvided "dynamictrees:DynamicTrees:1.12.2:0.9.5"
    deobfProvided "vazkii.botania:Botania:r1.10-363.119"
    deobfProvided "betteranimalsplus:betteranimalsplus:1.12.2:8.1.1"
    deobfProvided "curse.maven:quark:2759240"
    deobfProvided "curse.maven:autoreglib:2746011"
    deobfProvided "curse.maven:mowzies-mobs:2699705"
    deobfProvided "curse.maven:chisel:2809394"
    deobfProvided "curse.maven:ctm:2809915"
    deobfProvided "curse.maven:consecration:2837069"
    deobfCompile "net.ilexiconn:llibrary:1.7.9-1.12.2"
    compile "baubles:Baubles:1.12:1.5.2"
    compile "vazkii.patchouli:Patchouli:1.0-20.99"
    shade('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        transitive = false
    }
    implementation 'org.jetbrains:annotations:15.0'
}

jar {
    manifest.attributes(
            'FMLCorePlugin': 'com.bewitchment.core.BewitchmentFMLLoadingPlugin',
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.bewitchment.json',
            'FMLCorePluginContainsFMLMod': 'true',
            'ForceLoadAsMod': 'true'
    )

    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'

            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'

            exclude '**/*.html'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

mixin {
    add sourceSets.main, "mixins.bewitchment.refmap.json"
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
        refMap = "mixins.bewitchment.refmap.json"
    }
}
